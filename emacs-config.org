#+TITLE: emacs.d
#+STARTUP: showeverything
#+PROPERTY: header-args :cache yes :tangle yes :noweb yes

This is my GNU Emacs config file. There are many like it, but this one is mine.

** Early init

Enable [[https://www.gnu.org/software/emacs/manual/html_node/elisp/Lexical-Binding.html][lexical binding]] to speed up Lisp evaluation.

#+begin_src elisp
  ;; -*- lexical-binding: t; -*-
#+end_src

Temporarily increase the number of bytes of cons'ing we can do before garbage collection kicks in. We don't want to run the GC until after we finish initializing.

These tricks were adapted from [[https://github.com/hlissner/doom-emacs/blob/665b627b7c07c8d29ec8d334588cecc2ba308248/docs/faq.org#how-does-doom-start-up-so-quickly][Doom Emacs]].

#+begin_src elisp
  (setq gc-cons-threshold most-positive-fixnum
        gc-cons-percentage 0.6)

  (add-hook 'emacs-startup-hook (lambda () (setq gc-cons-threshold 16777216 ; 16MB
                                                 gc-cons-percentage 0.1)
                                  (garbage-collect)))
#+end_src

Temporarily unset =file-name-handler-alist= until we're done with init.

#+begin_src elisp
  (defvar my--file-name-handler-alist file-name-handler-alist)
  (setq file-name-handler-alist nil)

  (add-hook 'emacs-startup-hook (lambda ()
                                  (setq file-name-handler-alist my--file-name-handler-alist)))
#+end_src

Native compilation will likely become part of vanilla Emacs starting in version 28. In the event that we do support it, we'll disable deferred compilation mode.

#+begin_src elisp
  (setq comp-deferred-compilation nil)
#+end_src

Call =package-initialize= very early, even before we get to the package management section. Newer versions of Emacs activate installed packages /before/ loading the init file, so we don't need to call it in that case.

#+begin_src elisp
  (when (< emacs-major-version 27)
    (package-initialize))
#+end_src

By default, stuff added by Customize will show up in =init.el=, or whatever is specified by =C-h v user-init-file=. We want our =init.el= to remain simple and uncluttered, and we don't want it to contain changes that would show up in version control.

#+begin_src elisp
  (setq custom-file (concat user-emacs-directory "custom.el"))
  (when (file-exists-p custom-file)
    (load custom-file :noerror))
#+end_src

If I need to, I can define variables containing sensitive or platform-specific information and put them into a separate file that shouldn't be checked into version control. My =.gitignore= for this repo blacklists all files except for the ones I choose to commit, and =secrets.el= is not one of them.

This does mean that I need to remember to print an error message for myself if I forget to define anything that I use later in this file.

#+begin_src elisp
  (let ((secrets-file (concat user-emacs-directory "secrets.el")))
    (when (file-exists-p secrets-file)
      (load secrets-file :noerror)))
#+end_src

** Package management

Add MELPA package archives, and refresh/install [[https://github.com/jwiegley/use-package][use-package]] if it's not already available.

Whenever possible, I try to defer package loading so that my startup times are as fast as possible. =use-package='s documentation was a bit confusing at first, but eventually I got it figured out:

 - If you use any of =:bind=, =:mode=, or =:commands=, you don't have
   to manually =:defer t=.
 - You can benchmark how fast your packages load by setting
   =use-package-compute-statistics= before loading =use-package=
   itself, and then invoking =M-x use-package-report=.
 - Most packages, and especially ones that add support for new
   programming languages, are already set up to autoload, so you can
   add =:defer t= instead of figuring out a suitable =:mode= for them.
 - Otherwise, try [[https://emacs.stackexchange.com/a/19718][checking the function]] you want to invoke from that
   package, and if it's natively autoloaded, you can add =:defer t=.

=magit= was the only notable exception to this rule; even though I specified a =:bind=, I still had to add a =:defer t=.

#+begin_src elisp
  (unless (assoc-default "melpa" package-archives)
    (add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/") t))

  (unless (package-installed-p 'use-package)
    (package-refresh-contents)
    (package-install 'use-package))

  (setq use-package-verbose t)
  (setq use-package-always-ensure t)
#+end_src

** Utility packages

[[https://github.com/myrjola/diminish.el][diminish.el]] hides modes (or abbreviates their names) from the modeline.

#+begin_src elisp
  (use-package diminish
    :config (diminish 'eldoc-mode))
#+end_src

=sudo-edit= allows you to enter your =sudo= password and edit (shocking, I know!) read-only files. Just invoke =M-x sudo-edit=.

#+begin_src elisp
  (use-package sudo-edit
    :defer t)
#+end_src

** General behavior

By default, Emacs saves backup files in the current directory. These are the files ending in =~= that are cluttering up your directory lists. The following code stashes them all in =~/.emacs.d/backups=, where I can find them with =C-x C-f= (=find-file=) if I really need to.

#+begin_src elisp
  (unless backup-directory-alist
    (setq backup-directory-alist
          `(("." . ,(concat user-emacs-directory "backups")))))

  (setq auto-save-file-name-transforms
        `((".*" ,(concat user-emacs-directory "auto-save-list") t)))
#+end_src

Disable the creation of lockfiles, since I never have multiple users accessing the same instance of Emacs at the same time.

#+begin_src elisp
  (setq create-lockfiles nil)
#+end_src

Whenever I'm in =text-mode=, visually wrap lines without actually breaking them like =auto-fill-mode= would.

#+begin_src elisp
  (remove-hook 'text-mode-hook #'turn-on-auto-fill)
  (add-hook 'text-mode-hook 'turn-on-visual-line-mode)
#+end_src

Whenever I open a buffer, or do anything really, assume I want UTF-8 encoding instead of something silly like Latin-1.

#+begin_src elisp
  (set-charset-priority 'unicode)
  (setq locale-coding-system 'utf-8)
  (set-terminal-coding-system 'utf-8)
  (set-keyboard-coding-system 'utf-8)
  (set-selection-coding-system 'utf-8)
  (prefer-coding-system 'utf-8)
  (when (display-graphic-p)
    (setq x-select-request-type '(UTF8_STRING COMPOUND_TEXT TEXT STRING)))
#+end_src

Change "yes or no" to "y or n", because it's less typing.

#+begin_src elisp
  (fset 'yes-or-no-p 'y-or-n-p)
#+end_src

If I start typing while I have a selection active, delete the selection like every other editor would do.

#+begin_src elisp
  (delete-selection-mode t)
#+end_src

Use =display-line-numbers-mode= if it exists (Emacs 26+), otherwise use the deprecated =linum-mode=.

#+begin_src elisp
  (if (fboundp 'global-display-line-numbers-mode)
      (add-hook 'prog-mode-hook #'display-line-numbers-mode)
    (add-hook 'prog-mode-hook #'linum-numbers))
#+end_src

Don't display the "Welcome to Emacs" stuff, just start in the =*scratch*= buffer. Also, make the scratch buffer empty.

#+begin_src elisp
  (setq inhibit-startup-screen t
        initial-scratch-message nil)
#+end_src

(Some of these options were adapted nearly verbatim from [[https://github.com/technomancy/better-defaults][the better-defaults project]].)

Use =ibuffer= for =C-x C-b=, which will sensibly replace the current buffer rather than forcing you to move your cursor to a completely different buffer when all you wanted to do was change the current buffer in the current frame, gosh darn it!

#+begin_src elisp
  (global-set-key (kbd "C-x C-b") 'ibuffer)
#+end_src

Prefer RegEx-aware search functions. The originals are still available via the =C-M= prefix.

#+begin_src elisp
  (global-set-key (kbd "C-s") 'isearch-forward-regexp)
  (global-set-key (kbd "C-r") 'isearch-backward-regexp)
  (global-set-key (kbd "C-M-s") 'isearch-forward)
  (global-set-key (kbd "C-M-r") 'isearch-backward)
#+end_src

Automatically refresh the buffer if the file is changed externally. It's a pain to run something like =clang-format= and then not get to see the updated buffer. Do the same thing for Dired, and for ibuffer.

#+begin_src elisp
  (global-auto-revert-mode t)
  (setq dired-auto-revert-buffer t)
  (add-hook 'ibuffer-mode-hook (lambda () (ibuffer-auto-mode 1)))
#+end_src

Highlight pairs of parentheses.

#+begin_src elisp
  (show-paren-mode 1)
#+end_src

Disables =indent-tabs-mode= by default, so that indentation never inserts tabs. Other buffers (probably just =makefile-mode=) are still able to override this behavior if needed.

#+begin_src elisp
  (setq-default indent-tabs-mode nil)
#+end_src

The following code will:

 1. Put externally-copied clipboard text in the kill buffer before
    accidentally clobbering it.
 2. Make =apropos= commands search more extensively, even though it
    might be slightly slower.
 3. Make the mouse yank at point, rather than at click.
 4. Ensure files have a final newline appended when they are saved.
 5. Flash the frame rather than ringing a "bell" to get your
    attention.
 6. Prefer the newest Emacs Lisp byte code if there's multiple
    versions available.
 7. Make =ediff= display everything in a single frame rather than
    creating a new one.

#+begin_src elisp
  (setq save-interprogram-paste-before-kill t
        apropos-do-all t
        mouse-yank-at-point t
        require-final-newline t
        visible-bell t
        load-prefer-newer t
        ediff-window-setup-function 'ediff-setup-windows-plain)
#+end_src

[[https://github.com/justbur/emacs-which-key][which-key]] displays a list of available keybindings whenever you enter a partial command prefix. For example, if I press =C-x= and then give no more input, after a fraction of a second the minibuffer will open and show me everything that I can do with that prefix.

Knowing what's even possible with this editor is critical to understanding how to use it effectively. If you don't know what keybindings exist, how will you ever know when to use them? I wish I had installed this package sooner.

#+begin_src elisp
  (use-package which-key
    :diminish
    :config (which-key-mode))
#+end_src

[[https://github.com/minad/vertico][vertico]] is an alternative to Helm, Ido, and Ivy for making incremental, narrowing selections. The vanilla Emacs completion UI is a bit lackluster, so this package helps with selecting items -- files, buffers, commands, etc. -- from lists. It sounds mundane when you describe it that way, but good selection UIs are crucial for a pleasant editor experience.

It also uses the standard Emacs APIs, so there's no further configuration necessary like in Helm. Just install, enable the mode, and it's there.

Things I need to remember when I use this package:

 - To navigate into a directory while finding a file, press =TAB= or
   =C-i=, /not/ =RET=.

#+begin_src elisp
  (use-package vertico
    :init (vertico-mode)
    :custom (vertico-cycle t))
#+end_src

Use the [[https://github.com/oantolin/orderless][orderless]] completion style.

#+begin_src elisp
  (use-package orderless
    :custom
    (completion-styles '(orderless))
    (completion-category-defaults nil)
    (completion-category-overrides '((file (styles . (partial-completion))))))
#+end_src

Disable the automatic =*Completions*= buffer, and make tab-completion less noisy.

#+begin_src elisp
  (advice-add #'vertico--setup :after
              (lambda (&rest _)
                (setq-local completion-auto-help nil
                            completion-show-inline-help nil)))
#+end_src

Fix =org-set-tags-command=.

#+begin_src elisp
  (defun my/disable-selection ()
    (when (eq minibuffer-completion-table #'org-tags-completion-function)
      (setq-local vertico-map minibuffer-local-completion-map
                  completion-cycle-threshold nil
                  completion-styles '(basic))))
  (advice-add #'vertico--setup :before #'my/disable-selection)
#+end_src

Allow minibuffer commands while in the minibuffer.

#+begin_src elisp
  (setq enable-recursive-minibuffers t)
#+end_src

Disallow the cursor in the minibuffer prompt.

#+begin_src elisp
  (setq minibuffer-prompt-properties
        '(read-only t cursor-intangible t face minibuffer-prompt))
  (add-hook 'minibuffer-setup-hook #'cursor-intangible-mode)
#+end_src

Add [[https://github.com/minad/marginalia][marginalia]] for extra info in the minibuffer.

#+begin_src elisp
  (use-package marginalia
    :bind (:map minibuffer-local-map
                ("M-A" . marginalia-cycle))
    :init
    (marginalia-mode))
#+end_src

** Appearance

Disable scrollbars and the tacky menu bar. Without them, we can almost pretend this is a lean and modern editor!

#+begin_src elisp
  (unless (eq window-system 'ns)
    (menu-bar-mode -1))
  (when (fboundp 'tool-bar-mode)
    (tool-bar-mode -1))
  (when (fboundp 'scroll-bar-mode)
    (scroll-bar-mode -1))
  (when (fboundp 'horizontal-scroll-bar-mode)
    (horizontal-scroll-bar-mode -1))
#+end_src

Allow custom themes to load unhindered.

#+begin_src elisp
  (setq custom-safe-themes t)
#+end_src

Define a function that allows us to query the fonts we have installed, so that we can gracefully fall back from custom fonts to default system fonts.

#+begin_src elisp
  (defun my/first-matched-element (needles haystack predicate)
    "Returns the first matched needle in the haystack, or nil if there are none."
    (when (or (nlistp needles) (nlistp haystack) (not (functionp predicate)))
      (error "Expected parameters <list> <list> <function>"))

    (catch 'return
      (dolist (needle needles values)
        (let ((value (funcall predicate needle haystack)))
          (when value
            (throw 'return needle))))
      (throw 'return nil)))

  (defun my/get-installed-font (font-families)
    (my/first-matched-element font-families (font-family-list) 'member-ignore-case))
#+end_src

Define a helper function for taking an arbitrary color hex string and lightening it slightly.

#+begin_src elisp
  (defun my/lighten-color (color-hex-string)
    "Crudely lightens a color hex string."
    (let ((hex-number (string-to-number (substring color-hex-string 1) 16)))
      (format "#%x" (lsh (logand hex-number #x7f7f7f) 1))))
#+end_src

I've found that Emacs doesn't reliably load custom themes when started in daemon mode. For example, if I open =emacsclient=, I see my custom theme just fine, but the code that configures =whitespace-mode='s style doesn't get the right colors because Emacs somehow thought the background color was pure black. So we specify a function that handles everything to do with our custom themes, and we ensure it gets run only after the daemon has finished loading. See [[https://stackoverflow.com/a/23668935][here]] for more info.

#+begin_src elisp
  (defun my/apply-themes-and-styles ()
    "Loads and applies the custom themes."
    (interactive)
#+end_src

Emacs font heights are specified in 1/10 pt, so multiply your desired point size by 10 and you get your Emacs font height.

We also change the default fonts if we're on Windows, because Courier New is an abomination. Consolas has been around since Windows Vista (circa 2007), so it always ought to be available. We trust other operating systems to have a sane default font.

#+begin_src elisp
  (set-face-attribute 'default nil :height 115)
  (set-face-attribute 'variable-pitch nil :height 115)
  (set-face-attribute 'fixed-pitch nil :height 115 :family (face-attribute 'default :family))

  (when (string-equal system-type "windows-nt")
    (set-face-attribute 'default nil :family "Consolas")
    (set-face-attribute 'variable-pitch nil :family "Segoe UI")
    (set-face-attribute 'fixed-pitch nil :family "Consolas"))

  (let* ((serif-fonts '("Source Serif 4" "Apple Garamond" "Baskerville"
                        "Libre Baskerville" "Times New Roman" "Droid Serif"))
         (variable-font (my/get-installed-font serif-fonts)))
    (when variable-font
      (set-face-attribute 'variable-pitch nil :family variable-font)))
#+end_src

Install and load the color scheme, as well as a simpler, distraction-free modeline that respects that color scheme.

#+begin_src elisp
  (use-package base16-theme
    :config (load-theme 'base16-gruvbox-dark-hard t))

  (use-package smart-mode-line
    :config (setq sml/theme 'respectful)
    (smart-mode-line-enable))
#+end_src

The default =whitespace-mode= theme is hideously ugly, so we change it to blend in with our custom theme. Note that we have to ensure =whitespace.el= loads first, otherwise the faces that we're trying to modify won't be loaded yet.

#+begin_src elisp
  (with-eval-after-load 'whitespace
    (let ((lighter-background (my/lighten-color (face-attribute 'default :background)))
          (faces '(whitespace-indentation whitespace-space-after-tab whitespace-space-before-tab
                                          whitespace-space whitespace-tab whitespace-newline
                                          whitespace-empty whitespace-line)))

      (dolist (face faces)
        (set-face-attribute face nil :background nil :foreground lighter-background))
      (set-face-attribute 'whitespace-trailing nil :background lighter-background :foreground nil)))
#+end_src

Theme Org-mode so that it's more writing-oriented. See [[https://zzamboni.org/post/beautifying-org-mode-in-emacs/][here]]. Although I enjoyed the look of the variable-pitch font, I found it was brittle to set up and often conflicted with other parts of Emacs (tabs and minibuffer).

Once again, we need to ensure =org.el= is loaded before we can customize its faces and colors.

#+begin_src elisp
  (when (> emacs-major-version 27)
    (with-eval-after-load 'bookmark
      (set-face-attribute 'bookmark-face nil :background nil :foreground nil :inherit 'default)))
#+end_src

#+begin_src elisp
  ) ; End of my/apply-themes-and-styles
#+end_src

Finally, if we're loading in daemon mode, defer loading the themes; otherwise, just do it immediately.

#+begin_src elisp
  (if (daemonp)
      (add-hook 'after-make-frame-functions
                (lambda (frame)
                  (with-selected-frame frame
                    (my/apply-themes-and-styles))))
    (my/apply-themes-and-styles))
#+end_src

Make Org-mode WYSIWYG.

#+begin_src elisp
  (setq org-hide-emphasis-markers t)
  (add-hook 'buffer-face-mode-hook (lambda () (diminish 'buffer-face-mode)))
#+end_src

** General editing

Delete trailing whitespace when saving. The only time this gets in the way is when I'm editing someone else's poorly-formatted source code, and the stripped trailing whitespace shows up in version control.

#+begin_src elisp
  (add-hook 'before-save-hook 'delete-trailing-whitespace)
#+end_src

Show some whitespace when editing code, but not all whitespace.

#+begin_src elisp
  (setq whitespace-style '(face tabs spaces space-mark tab-mark))
  (add-hook 'prog-mode-hook 'whitespace-mode)
#+end_src

Sentences in Emacs are defined to end with two spaces instead of one by default. This means that when I use a command like =M-a= or =M-e= (backward and forward sentence, respectively) or =M-k= (kill to end of sentence), I end up deleting the entire paragraph, because I never end sentences with two spaces. This code changes it to be one space, like I expect.

The [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Sentences.html][GNU documentation]] notes that this may interfere with abbreviations, but I think this is an acceptable tradeoff. I'll never use sentence-based editing commands otherwise. In fact, I didn't even know about them until I saw it in [[https://pages.sachachua.com/.emacs.d/Sacha.html][someone else's]] config. I simply thought =M-e= moved to the end of the paragraph!

Also, I really need to get into the habit of using =C-x DEL= to delete backwards to the beginning of the sentence. It's quite handy, but I'm not yet in the habit of reaching for the sentence-based text manipulation keybindings (because of the aforementioned double-space weirdness.)

#+begin_src elisp
  (setq sentence-end-double-space nil)
#+end_src

This binds =M-Q= to be the inverse of =M-q= (=M-x fill-paragraph=).

#+begin_src elisp
  (defun my/unfill-paragraph (&optional region)
    "Take a multi-line paragraph and make it into a single line of text."
    (interactive (progn (barf-if-buffer-read-only) (list t)))
    (let ((fill-column (point-max))) (fill-paragraph nil region)))

  (global-set-key (kbd "M-Q") 'my/unfill-paragraph)
#+end_src

=multiple-cursors= lets you select and edit multiple pieces of text at once. It's rad.

#+begin_src elisp
  (use-package multiple-cursors
    :bind (("C-S-c C-S-c" . 'mc/edit-lines)
           ("C->" . 'mc/mark-next-like-this)
           ("C-<" . 'mc/mark-previous-like-this)
           ("C-c C-<" . 'mc/mark-all-like-this)))
#+end_src

=undo-tree= makes navigating Emacs' undo/redo history a breeze. You can view the tree with =C-x u= by default.

#+begin_src elisp
  (use-package undo-tree
    :diminish
    :config (global-undo-tree-mode))
#+end_src

=treemacs= is a tree-style file viewer, a common part of most IDEs and text editors.

#+begin_src elisp
  (use-package treemacs
    :defer t
    :init
    (with-eval-after-load 'winum
      (define-key winum-keymap (kbd "M-0") #'treemacs-select-window))
    :config
    (progn
      (setq treemacs-collapse-dirs                   (if treemacs-python-executable 3 0)
            treemacs-deferred-git-apply-delay        0.5
            treemacs-directory-name-transformer      #'identity
            treemacs-display-in-side-window          t
            treemacs-eldoc-display                   t
            treemacs-file-event-delay                5000
            treemacs-file-extension-regex            treemacs-last-period-regex-value
            treemacs-file-follow-delay               0.2
            treemacs-file-name-transformer           #'identity
            treemacs-follow-after-init               t
            treemacs-expand-after-init               t
            treemacs-git-command-pipe                ""
            treemacs-goto-tag-strategy               'refetch-index
            treemacs-indentation                     2
            treemacs-indentation-string              " "
            treemacs-is-never-other-window           nil
            treemacs-max-git-entries                 5000
            treemacs-missing-project-action          'ask
            treemacs-move-forward-on-expand          nil
            treemacs-no-png-images                   nil
            treemacs-no-delete-other-windows         t
            treemacs-project-follow-cleanup          nil
            treemacs-persist-file                    (expand-file-name ".cache/treemacs-persist" user-emacs-directory)
            treemacs-position                        'left
            treemacs-read-string-input               'from-child-frame
            treemacs-recenter-distance               0.1
            treemacs-recenter-after-file-follow      nil
            treemacs-recenter-after-tag-follow       nil
            treemacs-recenter-after-project-jump     'always
            treemacs-recenter-after-project-expand   'on-distance
            treemacs-litter-directories              '("/node_modules" "/.venv" "/.cask")
            treemacs-show-cursor                     nil
            treemacs-show-hidden-files               t
            treemacs-silent-filewatch                nil
            treemacs-silent-refresh                  nil
            treemacs-sorting                         'alphabetic-asc
            treemacs-select-when-already-in-treemacs 'move-back
            treemacs-space-between-root-nodes        t
            treemacs-tag-follow-cleanup              t
            treemacs-tag-follow-delay                1.5
            treemacs-text-scale                      nil
            treemacs-user-mode-line-format           nil
            treemacs-user-header-line-format         nil
            treemacs-wide-toggle-width               70
            treemacs-width                           35
            treemacs-width-increment                 1
            treemacs-width-is-initially-locked       t
            treemacs-workspace-switch-cleanup        nil)

      (treemacs-follow-mode t)
      (treemacs-filewatch-mode t)
      (treemacs-fringe-indicator-mode 'always)

      (pcase (cons (not (null (executable-find "git")))
                   (not (null treemacs-python-executable)))
        (`(t . t)
         (treemacs-git-mode 'deferred))
        (`(t . _)
         (treemacs-git-mode 'simple)))

      (treemacs-hide-gitignored-files-mode nil))

    :bind
    (:map global-map
          ("M-0"       . treemacs-select-window)
          ("C-x t 1"   . treemacs-delete-other-windows)
          ("C-x t t"   . treemacs)
          ("C-x t B"   . treemacs-bookmark)
          ("C-x t C-t" . treemacs-find-file)
          ("C-x t M-t" . treemacs-find-tag)))

  (use-package treemacs-icons-dired
    :hook (dired-mode . treemacs-icons-dired-enable-once))

  (use-package treemacs-magit
    :after (treemacs magit))
#+end_src

=nov.el= allows reading EPUBs in Emacs. Use =n= and =p= to navigate chapters, otherwise, standard Emacs keybindings apply.

#+begin_src elisp
  (use-package nov
    :if (executable-find "unzip")
    :mode ("\\.epub\\'" . nov-mode))
#+end_src

** Programming

Add the =company= auto-completion framework and its various integrations with other programming languages.

Adapted from [[https://github.com/jwiegley/dot-emacs/blob/master/init.el][jwiegley's]] config files.

#+begin_src elisp
  (use-package company
    :defer 3
    :diminish
    :commands (company-mode company-indent-or-complete-common)
    :init
    (dolist (hook '(emacs-lisp-mode-hook c-mode-common-hook))
      (add-hook hook
                #'(lambda ()
                    (local-set-key (kbd "<tab>")
                                   #'company-indent-or-complete-common))))
    :config (global-company-mode 1))
#+end_src

Git VCS.

#+begin_src elisp
  (use-package magit
    :defer t
    :bind ("C-x g" . 'magit-status))
#+end_src

Enable colors for =man= pages. See [[https://emacs.stackexchange.com/a/28925][here]].

#+begin_src elisp
  (require 'man)
  (set-face-attribute 'Man-overstrike nil :inherit font-lock-type-face :bold t)
  (set-face-attribute 'Man-underline nil :inherit font-lock-keyword-face :underline t)
#+end_src

Enable ANSI color interpretation in the compilation buffer. See [[https://stackoverflow.com/a/3072831][here]].

#+begin_src elisp
  (require 'ansi-color)
  (defun my/colorize-compilation-buffer ()
    (let ((inhibit-read-only t))
      (ansi-color-apply-on-region (point-min) (point-max))))
  (add-hook 'compilation-filter-hook 'my/colorize-compilation-buffer)
#+end_src

Make the compilation buffer scroll with its output.

#+begin_src elisp
  (setq compilation-scroll-output t)
#+end_src

LaTeX support.

#+begin_src elisp
  (use-package tex-mode
    :defer t
    :ensure auctex
    :config (add-hook 'TeX-after-compilation-finished-functions #'TeX-revert-document-buffer))
#+end_src

Markdown support, with special rules for when to prefer GitHub-flavored markdown.

#+begin_src elisp
  (use-package markdown-mode
    :commands (markdown-mode gfm-mode)
    :mode (("README\\.md\\'" . gfm-mode)
           ("\\.(md|markdown|mdown)\\'" . markdown-mode))
    :config (setq markdown-command "multimarkdown"))
#+end_src

JSON support.

#+begin_src emacs-lisp
  (use-package json-mode
    :defer t)
#+end_src

YAML support.

#+begin_src elisp
  (use-package yaml-mode
    :defer t)
#+end_src

TOML support.

#+begin_src elisp
  (use-package toml-mode
    :defer t)
#+end_src

CMake support.

#+begin_src elisp
  (use-package cmake-mode
    :defer t)
#+end_src

OpenGL shader language support.

#+begin_src elisp
  (use-package glsl-mode
    :mode ("\\.glsl(v|f)?\\'" "\\.vert\\'" "\\.frag\\'" "\\.geom\\'" "\\.(v|f)s\\'"))
#+end_src

Arch Linux PKGBUILDs should be treated like normal shell scripts.

#+begin_src elisp
  (add-to-list 'auto-mode-alist '("\\`PKGBUILD\\'" . shell-script-mode))
#+end_src

Make =M-;= produce C99+ line comments rather than block comments.

#+begin_src elisp
  (add-hook 'c-mode-hook (lambda () (setq comment-start "//" comment-end "")))
#+end_src

Set the default indent width to =4=.

#+begin_src elisp
  (setq-default c-basic-offset 4)
#+end_src

Use =clang-format= to format C/C++ source code. This relies on there being a =.clang-format= file somewhere in or above the current directory. You can generate one based on the default styles with a command like =clang-format -style=llvm -dump-config > .clang-format=, and then tweak it to suit your needs.

The keybinding will only be available in [[https://www.gnu.org/software/emacs/manual/html_mono/ccmode.html][CC Mode]] buffers. Note that we defer adding the binding to =c-mode-base-map= until after =c-initialization-hook= is run, because otherwise, that keymap won't be defined.

#+begin_src elisp
  (use-package clang-format
    :hook (c-initialization . (lambda () (define-key c-mode-base-map (kbd "C-c C-f") 'clang-format-buffer))))
#+end_src

Rust support.

#+begin_src elisp
  (use-package rust-mode
    :defer t)
#+end_src

Language server protocol mode, and its associated UIs. By default, try to use =rust-analyzer= (rather than the older RLS) as the default LSP implementation for Rust code. These are only enabled for Unix-like systems for now.

#+begin_src elisp
  (use-package lsp-mode
    :if (not (memq system-type '(ms-dos windows-nt cygwin)))
    :commands (lsp lsp-deferred)
    :hook (((rust-mode c-mode c++-mode) . lsp-deferred)
           (lsp-mode . lsp-enable-which-key-integration))
    :init (setq lsp-keymap-prefix "C-c y")
    :config (setq lsp-rust-server 'rust-analyzer))

  (use-package lsp-ui
    :commands lsp-ui-mode
    :bind ("M-RET" . 'lsp-ui-sideline-apply-code-actions))
#+end_src

Clojure and ClojureScript.

#+begin_src elisp
  (use-package clojure-mode
    :defer t)
#+end_src

** Org mode

Don't automatically indent the text to match the headers.

#+begin_src elisp
  (setq org-adapt-indentation nil)
#+end_src

When editing encrypted files, make sure they're not autosaved to disk unencrypted. The default value is to prompt, and I don't want to have to think about it. Also, ensure files are properly encrypted before being saved to disk.

#+begin_src elisp
  (setq org-crypt-disable-auto-save t)
  (org-crypt-use-before-save-magic)
  (setq org-tags-exclude-from-inheritance '("crypt"))
#+end_src

=org-store-link= can capture links to /files/, not just websites, and the links can be retrieved later with =C-c C-l= in Org mode. This is super useful for building links between different files and external websites in my Org database.

#+begin_src elisp
  (global-set-key (kbd "C-c l") 'org-store-link)
#+end_src

#+begin_src elisp
  (when (boundp 'secrets/org-directory)
    (setq org-directory secrets/org-directory))
  (setq org-default-notes-file (expand-file-name "inbox.org" org-directory))
#+end_src

=org-capture= is how I send and append text and data to my database of Org files.

#+begin_src elisp
  (global-set-key (kbd "C-c c") 'org-capture)

  (setq org-capture-templates `(("t" "Add unsorted data to the inbox" entry (file+headline org-default-notes-file "Inbox")
                                 "** %?\n")))
#+end_src

*** Org Roam

Basic config adapted from [[https://systemcrafters.net/build-a-second-brain-in-emacs/5-org-roam-hacks/][System Crafters]].

#+begin_src elisp
  (use-package org-roam
    :init
    (setq org-roam-v2-ack t)
    :custom
    (org-roam-directory (expand-file-name "roam" org-directory))
    (org-roam-completion-everywhere t)
    :bind (("C-c n l" . org-roam-buffer-toggle)
           ("C-c n f" . org-roam-node-find)
           ("C-c n i" . org-roam-node-insert)
           :map org-mode-map
           ("C-M-i" . completion-at-point)
           :map org-roam-dailies-map
           ("Y" . org-roam-dailies-capture-yesterday)
           ("T" . org-roam-dailies-capture-tomorrow))
    :bind-keymap
    ("C-c n d" . org-roam-dailies-map)
    :config
    (require 'org-roam-dailies) ;; Ensure the keymap is available
    (org-roam-db-autosync-mode))
#+end_src
